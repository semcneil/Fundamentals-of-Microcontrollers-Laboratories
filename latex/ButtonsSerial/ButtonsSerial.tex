\chapter{Buttons and Serial (UART)}
\chaplabel{buttonserial}

\section{Purpose}
The goal of this lab is to gain a better understanding of the serial interface between
the board and the computer, get the buttons all working, and add a few other fun interfaces. 

Note that the most time consuming part of this lab in 2022 was getting the non-SW1 switches
working.

\subsection{Serial Library}
\subsubsection{Initialization}
So far we have used the Serial library without much explanation of how to use it best.
In the sketches we have used we just have used the two lines shown in 
Listing~\ref{lst:simpleserialstart} to start Serial.

\begin{lstlisting}[language=C++, caption={If serial is required for a sketch this method of 
    starting Serial blocks until a serial monitor is started.},label={lst:simpleserialstart}]
    Serial.begin(115200);
    while(!Serial);
\end{lstlisting}

However, if a serial connection to the computer is not required, the code in 
Listing~\ref{lst:simpleserialstart} prevents the rest of the sketch from executing. The Serial
library does take some time to start so a simple solution is to just delay for a few seconds
after calling \lstinline$Serial.begin()$ as shown in Listing~\ref{lst:serialstartdelay}.

\begin{lstlisting}[language=C++, caption={If serial is not required for a sketch this method of 
    starting Serial waits a bit in hopes it connects.},label={lst:serialstartdelay}]
    Serial.begin(115200);
    delay(3000);
\end{lstlisting}

If you want to have the program quit waiting as soon as the Serial connects but not delay forever
like in Listing~\ref{lst:simpleserialstart}, you can keep checking for a serial connection a fixed
number of times as shown in Listing~\ref{lst:serialstartdelay}.

\begin{lstlisting}[language=C++, caption={This snippet tries connecting to Serial a fixed 
    number of times so that it will delay less than Listing~\ref{lst:serialstartdelay} if 
    a serial connection exists.},label={lst:serialstartcount}]
    const int maxNoSerial = 300;
    int noSerialCount = 0;
    while(!Serial && noSerialCount < maxNoSerial) {
      delay(10);
      noSerialCount++;
    }
\end{lstlisting}

\subsubsection{print vs println}
If you want to print a string with an endline, use \lstinline$Serial.println()$. This is 
handy for statements like \lstinline$Serial.println("Starting...")$. However, if you 
want to print out the value of variables you often don't want newline at the end of
the print. For this you use \lstinline$Serial.print()$. Some examples are shown in 
Listing~\ref{lst:serialprintprintln}.
\begin{lstlisting}[language=C++, caption={This snippet shows using print and println},label={lst:serialprintprintln}]
    Serial.print("Number of Names: ");
    Serial.println(nNames);
    ...
    Serial.print(F("You're connected to the network, IP = "));
    Serial.println(WiFi.localIP());    
\end{lstlisting}

\subsubsection{Reading data from the Serial}
The serial connection goes both ways. Information can be sent from the computer to 
your board. Listing~\ref{lst:serialreceiveexample} is an example of using input from 
the serial port. The call to \lstinline$Serial.available()$ tells the sketch whether
or not any characters have been received by the serial port. If characters have been
received by the serial port, \lstinline$Serial.read()$ reads them in one character at 
a time. The \lstinline$switch$ function allows different responses depending on what 
character is received. 

Note the layout of the sketch. It is important to define the pins for accessories so 
that they can be used later. 

\begin{lstlisting}[language=C++, caption={This sketch shows controlling parts of the 
    board using input from the serial port. Remember, don't copy and paste from a PDF
    since that process garbles some of the characters.},label={lst:serialreceiveexample}]
    /*  CEC325-SerialRcv.ino
    *  
    *  Demonstrates receiving and reacting to serial input.
    *  Uses the ERAU CEC325 board v0.3 which has an 
    *  Arduino Nano RP2040 Connect on board.
    *  
    *  Seth McNeill
    *  2022 February 09
    *  2022 September 20 modified for v0.5 board
    *  
    *  This code in the public domain.
    */
   
   #include <WiFiNINA.h>  // for RGB LED
   
   // pin definitions:
   #define RIGHT_BUTTON_PIN  A0 
   #define BUZZ_PIN          2 // buzzer
   
   
   void setup() {
     // initialize serial:
     Serial.begin(115200);
     while(!Serial) delay(100); // Require serial
     Serial.println("Starting...");
     
     // make the pins outputs:
     pinMode(LEDR, OUTPUT);  // WiFiNINA RGB LED red
     pinMode(LEDG, OUTPUT);  // WiFiNINA RGB LED green
     pinMode(LEDB, OUTPUT);  // WiFiNINA RGB LED blue
     pinMode(RIGHT_BUTTON_PIN, INPUT);  
     pinMode(LED_BUILTIN, OUTPUT);
     pinMode(BUZZ_PIN, OUTPUT);  // buzzer pin is output
   }
   
   void loop() {
     while(Serial.available() > 0) {  
       // characters have been received
       char inChar = Serial.read();
       switch(inChar) {
         case 'a': digitalWrite(LED_BUILTIN,HIGH); break;
         case 'A': digitalWrite(LED_BUILTIN,LOW); break;
         case 'b': digitalWrite(LEDB, HIGH); break;
         case 'r': digitalWrite(LEDR, HIGH); break;
         case 'g': digitalWrite(LEDG, HIGH); break;
         case '+': add(); break;
         case 'o': ledsOff(); break;
         case 'z': tone(BUZZ_PIN, 1000, 100); break;
         case '\n': break;
         default: Serial.print("Unknown character: "); Serial.println(inChar);
       }
     }
   }
   
   // turns off all 4 LEDs
   void ledsOff() {
     digitalWrite(LED_BUILTIN,LOW);
     digitalWrite(LEDR,LOW);
     digitalWrite(LEDG,LOW);
     digitalWrite(LEDB,LOW);
   }
   
   // Adds characters received subsequent to +
   void add() {
     Serial.println("Adding single digit numbers");
     int a = Serial.read() - 48; // subtract off value of ASCII 0
     int b = Serial.read() - 48; // subtract off value of ASCII 0
     Serial.print(a);
     Serial.print('+');
     Serial.print(b);
     Serial.print('=');
     Serial.println(a+b);
   }   
\end{lstlisting}


\subsection{Buttons}
\subsubsection{I/O Setup}
To use general I/O pins on a processor in Arduino the pin has to be defined as an 
input or an output. This is done in the \lstinline$setup()$ function using the 
\lstinline$pinMode()$ function. It has the form of 
\lstinline$pinMode(pinNumber, direction)$. The pinNumber is typically the number 
defined in the Arduino specifications as D1 or A0. If it is a D1 type number, then
just pass the number to pinMode. If it is an A0 type number, you have to specify 
both the letter and the number. So for the righthand button (SW1) on the v0.5 board 
that is attached to the A0 pin you would use \lstinline$pinMode(A0, INPUT)$ since
it is an input. The buzzer is attached to pin D2 so we would set it up as 
\lstinline$pinMode(2, OUTPUT)$ since it is an output. 

\subsubsection{Reading and Writing Pins}
Once the pinMode has been setup, you can read the value from an input using the 
\lstinline@digitalRead(pinNum)@ function which takes a pin number as an argument
and returns a 1 or 0 (LOW or HIGH) depending on the read value.

To write a value to an output pin, use the \lstinline@digitalWrite(pinNum, value)@ 
function. It also takes a pin number as an input along with whether you want the 
output LOW or HIGH. 

Since you will be using the pin number in multiple places, it is best to define it 
with a name at the top of the program so that you can change it at all places in 
your program by changing it once.

Examples of writing pins after setting their mode can be seen in 
Listing~\ref{lst:serialreceiveexample}.

\subsubsection{Other buttons}
The v0.5 board has 4 buttons. SW1 is attached to A0 and can be read directly using
\lstinline@digitalRead@. The other three buttons (SW2, FRONT\_SW, and BACK\_SW) are 
connected to the PCA9535 I2C to GPIO chip with an address of 0x20 and have to be 
accessed using the PCA95x5 library. 

%Remember to install the McNeill version of the PCA95x5 library from a Github zip 
%file, not the library from the standard Arduino library manager.
Remember to install the PCA95x5 library as follows:
In Sketch $\rightarrow$ Include Library $\rightarrow$ Manage Libraries search for 
PCA9535 and install the latest version of the one by hideakitai.

\begin{enumerate}
    \item Include the PCA95x5 library
    \item Create a global variable of type PCA9535 named something like \lstinline@muxU31@ 
            (for the chip listed as U31 on the schematic)
    \item Initialize the variable/object inside the \lstinline|setup()| function:
    \begin{enumerate}
        \item Start the wire library: \lstinline@Wire.begin()@
        \item Attach to the mux using the attach method and correct address: 
                \lstinline@muxU31.attach(Wire, 0x20)@
        \item Set the polarity: \lstinline@muxU31.polarity(PCA95x5::Polarity::ORIGINAL_ALL)@ 
        \item Set the direction: \lstinline@muxU31.direction(0x????)@ remembering that a 1 is 
                an input and a 0 is an output. Set non-connected pins as outputs. Change the 
                question marks appropriately by looking at the schematics in the Class Manual 
                linked to in the \nameref{sec:buttonserialresources} Section. Note that 
                the only outputs on U31 are Prox1ALRT and SHT31\_RST. Set pins that are not 
                connected as outputs too.
    \end{enumerate}
    \item Once the object is properly setup, read values with the \lstinline@read()@ 
            method. This returns a 16-bit number representing the 16 inputs on the chip.
    \item A quick way to check if a bit is high is to bitwise AND (\&) it with a number 
            where only the bit of interest is 1: \lstinline@mux31.read() & 0x0004@ reads 
            the third input.
\end{enumerate}

Note that switches SW1 and SW2 are are HIGH when not pressed and LOW 
when pressed while FRONT\_SW and BACK\_SW are LOW when not pressed and HIGH when pressed.

\begin{lstlisting}[language=C++, caption={The buttons attached to the PCA9535 can be accessed 
    as shown in this code snippet.},label={lst:muxButtons}]
    uint16_t muxVal = muxU31.read();
    if(muxVal & 0x0001) {
        // Do something with left button
    }
    if(muxVal & 0x0002) {
        // Do something with another button
    }
\end{lstlisting}



\subsection{Making Noise (buzzer)}
The board for lab has a buzzer attached to pin D2. Arduino has a handy function called 
\lstinline@tone(pin, frequency, duration)@. This is an easy way to make your board beep.
The duration is in milliseconds and the function blocks until the duration expires. Note 
that there is another version of the function with the form 
\lstinline@tone(pin, frequency)@ that does not specify a duration. This is a non-blocking
way to make tones. It will continue to make the tone until a call to \lstinline@noTone(pinNum)@
is called. 

\textbf{NOTE:} If the tone function is all that is in a loop, it needs a short delay (10~ms will do)
after it to keep the Nano Connect RP2040 from crashing.

\subsection{NeoPixels}
The board has 18 multicolor LEDs around its periphery. These are called NeoPixels by the 
Adafruit company. They get called other things by other companies. They are all based on 
the WS2812 type chip. We will use the Adafruit NeoPixel library. Be sure to install it 
in the usual manner in the Library Manager. It requires the usual:
\begin{enumerate}
    \item \#include the library
    \item Initialize an object
    \item Setup the library
    \item Use the library
\end{enumerate}
A minimal sketch to do all these is shown in Listing~\ref{lst:neopixelsetup}

\begin{lstlisting}[language=C++, caption={This snippet shows how to setup and run the NeoPixels on the board.},label={lst:neopixelsetup}]
    /* NeoPixelSetup.ino
    * 
    * A minimum sketch to setup NeoPixels.
    * 
    * Seth McNeill
    * 2022 September 20
    */
   
   #include <Adafruit_NeoPixel.h> // NeoPixel library
   
   #define NEO_PIN           17  // WARNING! THIS IS GPIO NOT D NUMBER for NeoPixels
   #define NEO_COUNT         18  // number of NeoPixels
   
   // Declare our NeoPixel strip object:
   Adafruit_NeoPixel strip(NEO_COUNT, NEO_PIN, NEO_GRB + NEO_KHZ800);
   
   void setup() {
     Serial.begin(115200);
   
     // Setup NeoPixels
     strip.begin();
     strip.clear();  // Set all pixel values to zero
     strip.show();   // Write values to the pixels
   }
   
   void loop() {
     for(int ii = 0; ii < NEO_COUNT; ii++) {
       // set all the pixels to purple
       strip.setPixelColor(ii, strip.Color(255,0,255));
     }
     strip.show();
     delay(1000);
     // turn off all the LEDs
     strip.clear();
     strip.show();
     delay(1000);
   }   
\end{lstlisting}

\section{Resources}\label{sec:buttonserialresources}
\begin{enumerate}
    \item \href{https://www.arduino.cc/reference/en/language/functions/communication/serial/}{Serial Library}
    \item \href{https://www.arduino.cc/reference/en/language/functions/digital-io/digitalread/}{digitalRead}
    \item \href{https://www.arduino.cc/reference/en/language/functions/advanced-io/tone/}{tone}
    \item \href{https://www.arduino.cc/reference/en/language/functions/advanced-io/notone/}{notone}
%    \item \href{https://github.com/adafruit/Adafruit_SHT31}{Adafruit's SHT31 library}
%    \item \href{https://github.com/RobTillaart/SHT31}{Rob Tillaart's SHT31 library}
    \item \href{https://www.arduino.cc/reference/en/libraries/adafruit-neopixel/}{Adafruit NeoPixel Library}
    \item \href{https://www.nxp.com/docs/en/data-sheet/PCA9535_PCA9535C.pdf}{PCA9535 datasheet}
    \item \href{https://github.com/semcneil/PCA95x5}{PCA9535 library} (McNeill version)
    \item PCB Schematic and Layout - see 
            \href{https://github.com/semcneil/Fundamentals-of-Microcontrollers-Manual}{class manual} 
            in the Arduino Startup $\rightarrow$ Schematics and PCB section
\end{enumerate}

\section{Procedure}
Write a sketch with the following functionality:
\begin{enumerate}
    \item Reacts to all 4 buttons on the board in some way such as sound, light, or Serial. 
            THIS IS THE HARDEST PART. START HERE.
    \item Reads in values from the serial port and does something in response.
    \item Writes information to the serial port in response to some stimulus/stimuli.
    \item Creates a tone in response to some stimulus.
    \item Make the NeoPixels do something.
\end{enumerate}

\section{Debugging}
Here are some pointers to help if your program gives errors:
\begin{enumerate}
    \item Missing \lstinline|Wire.h|: Make sure you are including the PCA95x5 library
    \item Upload just gives a series of dots and says upload failed.
    \begin{enumerate}
        \item Check to make sure the correct COM port is selected in Tools menu
        \item Try pressing the reset button twice to put the RP2040 in upload mode 
        \item If nothing works, bring your board to the professor/TA to have a hard reset performed
    \end{enumerate}
    \item ``Out of scope'' errors. Count and match curly brackets (\{\}) to make sure all
                your code is inside \lstinline|setup()| or \lstinline|loop()|. The only code
                that can be outside of a function is a variable declaration (e.g. \lstinline|int x|)
\end{enumerate}

\section{Turn In}
Turn in the following:
\begin{enumerate}
    \item Make sure that the TA/Instructor signs off on your sketch demonstration.
    \item A PDF of your sketch.
    \item .ino versions of your sketch.
    \item Fill out the end of lab quiz prior to leaving. Note that it includes asking you 
            for the output of the \lstinline$getIDs$ sketch. 
\end{enumerate}